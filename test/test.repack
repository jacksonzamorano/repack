import "test-import.repack"

output description @test/description {
	print_commands true
}

output postgres @test/postgres #models #views;
output typescript_class @test/ts_classes #data {
	make_index true
}
output typescript_interface @test/ts_interfaces #data {
	make_index true
}
output typescript_drizzle @test/ts_drizzle #models;
output rust @test/rust_vanilla #data #private;

record User @users #private #models {
	id int32 #pk #generated #unique
	name string
	email string
	password string
	login_count int64
	last_login datetime
	total_cost float64
	org_id ref(Organization.id)
	personal_org_id ref(Organization.id)
}

record Organization @orgs #models #data {
	id int32 #pk #generated #unique
	name string
	email string
}

record Todo @todos #models #data {
	id int32 #pk #generated #unique
	done boolean
	title string
	description string
	creator_user_id ref(User.id)
	assigned_user_id ref(User.id)?
	list_id ref(List.id)?
	org_id ref(Organization.id)
}

record List @lists #models #data {
	id int32 #pk #generated #unique
	title string
	description string
	creator_user_id ref(User.id)
	org_id ref(Organization.id)
}

record UserPublic: User #data {
	*
	- password
}

record AllUserList: UserPublic #views {
	*
	org_name from(org_id.name)
	- personal_org_id
	- password
}
